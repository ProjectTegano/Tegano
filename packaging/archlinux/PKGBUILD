# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Maintainer: Andreas Baumann <abaumann at yahoo dot com>
pkgname=wolframe
pkgver=0.0.1
pkgrel=2
pkgdesc="A flexible client-server ecosystem for business applications."
license=('GPL3')
arch=('i686' 'x86_64')
url="http://wolframe.net/"
depends=('boost>=1.48' 'boost-libs>=1.48' 'openssl' 'pam' 'libsasl' 
         'sqlite3' 'postgresql-libs' 'libxml2' 'libxslt' 'libharu'
         'freeimage' 'python')
makedepends=('docbook-xsl' 'doxygen' 'fop' 'graphviz' 'dia')
backup=('etc/wolframe/wolframe.conf')
install='wolframe.install' 
source=(
  "http://sourceforge.net/projects/wolframe/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
  'wolframe.conf'
  'wolframed.service'
)

md5sums=('68b6fd14c81367ee90a8a603ee017fc2'
         '2fde608c00c6d098683db533cbaab07b'
         'cc146a28908aad55f156860340934095')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  msg "Installing.."
  make \
    WITH_SSL=1 WITH_EXPECT=1 WITH_PAM=1 WITH_SASL=1 \
    WITH_SYSTEM_SQLITE3=1 WITH_PGSQL=1 WITH_LUA=1 WITH_LIBXML2=1 \
    WITH_LIBXSLT=1 WITH_SYSTEM_LIBHPDF=1 WITH_ICU=1 \
    WITH_SYSTEM_FREEIMAGE=1 WITH_PYTHON=1 WITH_CJSON=1 \
    WITH_TEXTWOLF=1 RELEASE=1 \
    DESTDIR=${pkgdir} prefix=/usr sbindir=/usr/bin \
    sysconfdir=/etc \
    install

  install -D -m644 ${srcdir}/wolframe.conf \
    ${pkgdir}/etc/wolframe/wolframe.conf
  install -D -m644 ${srcdir}/wolframed.service \
    ${pkgdir}/usr/lib/systemd/system/wolframed.service
  install -d -m775 -g nobody -o nobody \
    ${pkgdir}/var/log/wolframe
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  msg "Generating dependencies..."
  make depend \
    WITH_SSL=1 WITH_EXPECT=1 WITH_PAM=1 WITH_SASL=1 \
    WITH_SYSTEM_SQLITE3=1 WITH_PGSQL=1 WITH_LUA=1 WITH_LIBXML2=1 \
    WITH_LIBXSLT=1 WITH_SYSTEM_LIBHPDF=1 WITH_ICU=1 \
    WITH_SYSTEM_FREEIMAGE=1 WITH_PYTHON=1 WITH_CJSON=1 \
    WITH_TEXTWOLF=1 RELEASE=1 \
    DEFAULT_MODULE_LOAD_DIR=/usr/lib/wolframe/modules \
    CFLAGS='-O2' CXXFLAGS='-O2' \
    LDFLAGS=-Wl,-rpath=/usr/lib/wolframe,-rpath=/usr/lib/wolframe/plugins

  msg "Building..."
  make \
    WITH_SSL=1 WITH_EXPECT=1 WITH_PAM=1 WITH_SASL=1 \
    WITH_SYSTEM_SQLITE3=1 WITH_PGSQL=1 WITH_LUA=1 WITH_LIBXML2=1 \
    WITH_LIBXSLT=1 WITH_SYSTEM_LIBHPDF=1 WITH_ICU=1 \
    WITH_SYSTEM_FREEIMAGE=1 WITH_PYTHON=1 WITH_CJSON=1 \
    WITH_TEXTWOLF=1 RELEASE=1 \
    DEFAULT_MODULE_LOAD_DIR=/usr/lib/wolframe/modules \
    CFLAGS='-O2' CXXFLAGS='-O2' \
    LDFLAGS=-Wl,-rpath=/usr/lib/wolframe,-rpath=/usr/lib/wolframe/plugins

#  msg "Building documentation.."
#  cd docs
#  make doc
#  cd ..
}
